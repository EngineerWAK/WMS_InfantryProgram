/**
* InfantryProgram
*
* TNA-Community
* https://discord.gg/Zs23URtjwF
* Â© 2021 {|||TNA|||}WAKeupneo
*
* This work is licensed under the Creative Commons Attribution-NonCommercial-NoDerivatives 4.0 International License. 
* To view a copy of this license, visit http://creativecommons.org/licenses/by-nc-nd/4.0/.
* Do Not Re-Upload
*/

// [player,_pos,_amount] remoteExec ["TNA_fnc_InfantryProgram_BlackFish"];
// _pos is generated by cursorObject
private [];
params [
  "_target",
  "_pos",
  "_amount",
  "_plane"
];
if (WMS_InfantryProgram_LOGs) then {diag_log format ["[BLACKFISH SUPPORT]|TNA|TNA|TNA|TNA|TNA| _this check, _this = %1 ", _this]};

private _timer = (300+(random 300));
private _lockerMoney = 0;

if (WMS_exileFireAndForget) then {
  _lockerMoney = _target getVariable ['ExileLocker', 0];
}else {
  _lockerMoney = _target getVariable ['ExileMoney', 0];
};

if(_lockerMoney > _amount) then { 
  if (WMS_exileFireAndForget) then {
    //////////EXILE
		_lockerMoney1 = _target getVariable ['ExileLocker', 0];
    _playerMoney = _target getVariable ['ExileMoney', 0];
		_lockerMoney = _lockerMoney1 - _amount;
		_poptabsplayer = _playerMoney + _lockerMoney;
		_target setVariable ['ExilePopTabsCheck', _poptabsplayer];
		_target setVariable ['ExileMoney', _playerMoney, true];
		_target setVariable ['ExileLocker', _lockerMoney, true];
    format['updateLocker:%1:%2', _lockerMoney, (getPlayerUID _target)] call ExileServer_system_database_query_fireAndForget;
  } else {
				_ownerUID = getPlayerUID _target;
				_playerUID_ExileMoney = "ExileMoney_"+_ownerUID;
				_playerMoney = profileNamespace getVariable [_playerUID_Exilemoney,0];
				profileNamespace setVariable [_playerUID_Exilemoney,(_playerMoney-_amount)];
				_target setVariable ["ExileMoney", (_playerMoney-_amount), true];

  };
 
private _RandomPosSpawn = [_target, 1200, 2200] call BIS_fnc_findSafePos;
private _vhl = [_RandomPosSpawn, (0+random 359), _plane, BLUFOR] call bis_fnc_spawnvehicle;
private _RandomPosEnd = [_pos, worldSize, (worldSize*1.2)] call BIS_fnc_findSafePos;      
private _vehic = (_vhl select 0);
private _grp = (_vhl select 2);
clearMagazineCargoGlobal _vehic;
clearWeaponCargoGlobal _vehic;
clearItemCargoGlobal _vehic;
clearBackpackCargoGlobal _vehic;

_vehic addMagazineTurret       ["5000Rnd_762x51_Yellow_Belt",[2]]; 
_vehic addWeaponTurret         ["M134_minigun",[2]];

_vehic lockDriver true;
_vehic setVehicleLock "LOCKEDPLAYER"; 
_vehic flyInHeight 300;
if (worldName == "Tanoa") then {_vehic flyInHeight 400};

WMS_BlackFishSupport_Last = time;
publicVariable "WMS_BlackFishSupport_Last";

{
  removeAllItems _x;
  removeAllWeapons _x;
  removeBackpackGlobal _x;
  _x additem "FirstAidKit";
  //_x disableAI "AUTOTARGET";
  //_x disableAI "TARGET";
  _x disableAI "AUTOCOMBAT";
} forEach units _grp ;

private _markerPos = _pos; 
private _markerName = "Grid" + (str _markerPos);  
private _markerName2 = "BlackFish" + (str _markerPos);
private _BlackFishSupport = createMarker [_markerName, _markerPos]; 
_BlackFishSupport setMarkerShape "ELLIPSE";
_BlackFishSupport setMarkerBrush "DIAGGRID"; 
_BlackFishSupport setMarkerSize [200, 200];
_BlackFishSupport setMarkerType "select"; 
_BlackFishSupport setMarkercolor "ColorBlue";

private _BlackFishSupport2 = createMarker [_markerName2, _markerPos]; 
_BlackFishSupport2 setMarkerShape "ICON";
_BlackFishSupport2 setMarkerType "o_motor_inf"; 
_BlackFishSupport2 setMarkercolor "ColorBlack";
_BlackFishSupport2 setMarkerText  "BlackFish Support";

["toastRequest", ["InfoTitleAndText", [format ["%1 Air Support on the way",(typeof _vehic)]]]] call ExileServer_system_network_send_broadcast;

private _WPT_AirSupport1 = _grp addWaypoint [position _target, 150];
_WPT_AirSupport1 setWaypointType "MOVE";
_WPT_AirSupport1 setWaypointCombatMode "BLUE";
_WPT_AirSupport1 setWaypointbehaviour  "CARELESS";

private _WPT_AirSupport2 = _grp addWaypoint [_pos, 500];
_WPT_AirSupport2 setWaypointType "LOITER";
_WPT_AirSupport2 setWaypointLoiterType "CIRCLE_L";
_WPT_AirSupport2 setWaypointLoiterRadius 600;
_WPT_AirSupport2 setWaypointSpeed "LIMITED"; //NORMAL
_WPT_AirSupport2 setWaypointCombatMode "RED";
_WPT_AirSupport2 setWaypointbehaviour  "COMBAT";

WMS_AI_bluforPatrol_Running pushback [time,(time+_Timer),[_grp],[_vehic],[],[_markerName, _markerName2],[_WPT_AirSupport1, _WPT_AirSupport2],"BLACKFISH"];
/*
waituntil  {_vehic distance2d _pos <= 1000};
uisleep _timer;
["toastRequest", ["InfoTitleAndText", [format ["%1 Mission done, leaving the combat zone",(typeof _vehic)]]]] call ExileServer_system_network_send_broadcast;
_WPT_AirSupport2 setWaypointSpeed "FULL"; //Doesnt change anything
_vehic flyInHeight 2000;
uisleep 120;
{deletevehicle _x} foreach units _grp;
_vehic setdamage 0.95;
uisleep 10;
[_grp,[_vehic],[_markerName, _markerName2],[_WPT_AirSupport1, _WPT_AirSupport2]] call TNA_fnc_lvl2_cleanup; //[_group,[_vehicles],[_markers],[_waypoint]] call TNA_fnc_Ivl2_cleanup;
*/

} else {systemChat "Nope! you are too poor!"};
